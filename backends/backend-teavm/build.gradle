apply plugin: "java"
apply plugin: 'java-library'

apply from: '../../dependencies.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets.main.java.srcDirs = [ "emu", "src/main/java/" ]
sourceSets.main.resources.srcDirs = [ "src/main/resources/" ]

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


javadoc {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

configurations {
    provided
    implementation.extendsFrom provided
}

dependencies {
    api project(":backends:backend-web")
    api fileTree(dir: 'libs', include: ['*.jar'])
    provided "com.badlogicgames.gdx:gdx:$gdxVersion"
}

task fromClasses (type: Jar) {
    from (sourceSets.main.output){
    }
}

task platformAll (type: Jar, dependsOn: [':backends:dragome-backend:build']) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from
    {
        (configurations.runtimeClasspath + sourceSets.main.output - configurations.provided).collect
        {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    {
        exclude ("gdx-" + gdxVersion + ".jar")
        exclude "*.template"
        exclude "*.idl"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/maven/"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            version project.gdxHtmlVersion + project.gdxHtmlType
            project.version = project.gdxHtmlVersion + project.gdxHtmlType
            artifactId "dragome-backend"
            artifact fromClasses
        }
        allBuild(MavenPublication) {
            version project.gdxHtmlVersion + "-ALL" + project.gdxHtmlType
            project.version = project.gdxHtmlVersion + "-ALL" + project.gdxHtmlType
            artifactId "dragome-backend"
            artifact platformAll
        }
    }
}