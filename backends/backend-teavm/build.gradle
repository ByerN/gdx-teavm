plugins {
    id 'java-library'
}

group = project.groupId
version = project.gdxTeaVMVersion

def module_name = "backend-teavm"

sourceSets.main.java.srcDirs = [ "emu", "src/main/java/"]
sourceSets.main.resources.srcDirs = ["src/main/resources/"]

dependencies {
    implementation "org.reflections:reflections:$project.reflectionVersion"
    implementation "com.badlogicgames.gdx:gdx:$project.gdxVersion"

    api "org.teavm:teavm-tooling:$project.teaVMVersion"
    api "org.teavm:teavm-core:$project.teaVMVersion"
    api "org.teavm:teavm-classlib:$project.teaVMVersion"
    api "org.teavm:teavm-jso:$project.teaVMVersion"
    api "org.teavm:teavm-jso-apis:$project.teaVMVersion"
    api "org.teavm:teavm-jso-impl:$project.teaVMVersion"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = module_name
            from components.java

            pom {
                name = "Gdx-teaVM"
                description = "Tool to generate libgdx to javascript using teaVM"
                url = "https://github.com/xpenatan/gdx-teavm"
                developers {
                    developer {
                        id = "Xpe"
                        name = "Natan"
                    }
                }
                scm {
                    connection = "scm:git:git://https://github.com/xpenatan/gdx-teavm.git"
                    developerConnection = "scm:git:ssh://https://github.com/xpenatan/gdx-teavm.git"
                    url = "http://https://github.com/xpenatan/gdx-teavm/tree/master"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
}

signing {
    def ver = project.version.toString()
    def isSnapshot = ver.toUpperCase().contains("SNAPSHOT")
    if(!isSnapshot) {
        def signingKey = System.getenv("signingKey")
        def signingPassword = System.getenv("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}