apply plugin: "java"
apply plugin: 'java-library'

group = project.groupId

def module_name = "backend-teavm-core"
def module_native_name = "backend-teavm-native"

sourceSets.main.java.srcDirs = [ "emu", "src/main/java/"]
sourceSets.main.resources.srcDirs = ["src/main/resources/"]

configurations {
    includeDep
    implementation.extendsFrom includeDep
}

dependencies {
    includeDep "org.reflections:reflections:$project.reflectionVersion"
    implementation project(":backends:backend-web:backend-web")
    api fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.badlogicgames.gdx:gdx:$project.gdxVersion"
}

jar {
    archivesBaseName = module_name
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources");
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

javadoc {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

task fromClasses(type: Jar) {
    from(sourceSets.main.output) {
    }
}

task addNative(type: Jar) {
    from zipTree("./libs/teavm-cli-0.7.0-dev-1200.jar")
}

publishing {
    publications {
        teavmNatives(MavenPublication) {
            version project.gdxHtmlVersion + project.gdxHtmlType
            project.version = project.gdxHtmlVersion + project.gdxHtmlType
            artifactId module_native_name
            artifact addNative
        }
        maven(MavenPublication) {
            version project.gdxHtmlVersion + project.gdxHtmlType
            project.version = project.gdxHtmlVersion + project.gdxHtmlType
            artifactId module_name
            artifact fromClasses

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.includeDep.allDependencies.each {
                    def dependencyNode = dependencies.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}