plugins {
    id 'java'
}

project.ext.mainClassName = "com.github.xpenatan.gdx.examples.bullet.Main"
sourceSets.main.java.srcDirs = ["src/main/java/"]
sourceSets.main.resources.srcDirs = ["../desktop/assets"]

dependencies {
    implementation project(":examples:bullet:core")
    implementation "com.badlogicgames.gdx:gdx-platform:$project.gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$project.gdxVersion"

    // teaVM Version
    implementation files("../../../extensions/gdx-bullet/gdx-bullet-build/jni/build/c++/libs/gdx-bullet-natives.jar")

    //GDX Version
//    implementation "com.badlogicgames.gdx:gdx-bullet-platform:$project.gdxVersion:natives-desktop"
}

tasks.register('runBulletDesktop', JavaExec) {
    dependsOn classes
    setGroup("examples-desktop")
    setDescription("Run bullet example")
    mainClass.set(project.mainClassName)
    setClasspath(sourceSets.main.runtimeClasspath)

    if (org.gradle.internal.os.OperatingSystem.current() == org.gradle.internal.os.OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

tasks.register('dist', Jar) {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}